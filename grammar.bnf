program               ::= decl* EOF ;

decl                  ::= var_decl | stmt ;
var_decl              ::= "var" IDENTIFIER ( "=" expr )? ";" ;

stmt                  ::= expr_stmt | print_stmt | block_stmt | if_stmt | while_stmt | for_stmt;
expr_stmt             ::= expr ";" ;
print_stmt            ::= "print" expr ";" ;
block_stmt            ::= "{" decl* "}" ;
if_stmt               ::= "if" "(" expr ")" stmt ( "else" stmt)? ;
while_stmt            ::= "while" "(" expr ")" stmt ;
for_stmt              ::= "for" "(" ( var_decl | expr_stmt | ";" ) expr? ";" expr? ")" stmt ;

expr                  ::= assignment_expr
assignment_expr       ::= IDENTIFIER '=' assignment_expr | comma_expr;
comma_expr            ::= conditional_expr (',' conditional_expr)* ;
conditional_expr      ::= logical_or_expr ('?' expr ':' expr)* ;
logical_or_expr       ::= logical_and_expr ("or" logical_and_expr)* ;
logical_and_expr      ::= equality_expr ("and" equality_expr)* ;
equality_expr         ::= comparison_expr  ( ( "!=" | "==" ) comparison_expr  )* ;
comparison_expr       ::= term_expr  ( ( '>' | ">=" | '<' | "<=" ) term_expr  )* ;
term_expr             ::= factor_expr ( ( '-' | '+' ) factor_expr  )* ;
factor_expr           ::= unary_expr ( ( '/' | '*' ) unary_expr )* ;
unary_expr            ::= ( '!' | '-' ) unary_expr | primary_expr ;
primary_expr          ::= NUMBER | STRING | "true" | "false" | "nil" | "(" expr ")" IDENTIFIER ;

NUMBER                ::= DIGIT+ ( "." DIGIT+ )? ;
STRING                ::= '"' <any_except('"')>* '"' ;
IDENTIFIER            ::= ALPHA ( ALPHA | DIGIT )* ;
ALPHA                 ::= 'a' .. 'z' | 'A' .. 'Z' | '_' ;
DIGIT                 ::= '0' .. '9' ;
